/*
 * Copyright 2020 Peter Kenji Yamanaka
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: "com.android.application"
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

//noinspection GroovyMissingReturnStatement
android {
	compileSdkVersion rootProject.ext.maxSdk

	//noinspection GroovyMissingReturnStatement
	defaultConfig {
		applicationId "com.pyamsoft.zaptorch"
		minSdkVersion rootProject.ext.minSdk
		targetSdkVersion rootProject.ext.maxSdk
		versionCode 106
		versionName "REL.20210118-1"

		// For vector drawables
		vectorDrawables.useSupportLibrary = true

		resConfigs "en"
	}

	kotlinOptions {
		jvmTarget = "1.8"
	}

	signingConfigs {
		debug {
			storeFile file("debug.keystore")
			keyAlias "androiddebugkey"
			keyPassword "android"
			storePassword "android"
		}

		release {
			storeFile new File(BUNDLE_STORE_FILE)
			keyAlias BUNDLE_KEY_ALIAS
			keyPassword BUNDLE_KEY_PASSWD
			storePassword BUNDLE_STORE_PASSWD
		}
	}

	buildTypes {
		release {
			//noinspection GroovyAssignabilityCheck
			signingConfig signingConfigs.release
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"),
					"proguard-rules.pro"
		}

		debug {
			signingConfig signingConfigs.debug
			applicationIdSuffix ".dev"
			versionNameSuffix "-dev"
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	buildFeatures {
		viewBinding true
	}

	// Fixes this error message
	// More than one file was found with OS independent path 'META-INF/core_release.kotlin_module'
	packagingOptions {
		pickFirst 'META-INF/core_release.kotlin_module'
		pickFirst 'META-INF/ui_release.kotlin_module'
	}
}

// Kapt flags
kapt {
	correctErrorTypes = true
	useBuildCache = true
	mapDiagnosticLocations = true

	javacOptions {
		option("-Xlint:unchecked")
		option("-Xlint:deprecation")
		option("-Xmaxerrs", 10000)
	}
}

dependencies {
	// Leak Canary
	debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanary"

	// PYDroid
	implementation "com.pyamsoft.pydroid:ui:$pydroid"
	debugImplementation "com.pyamsoft.pydroid:autopsy:$pydroid"

	// AndroidX
	implementation "androidx.coordinatorlayout:coordinatorlayout:$coordinatorLayout"
	implementation "androidx.constraintlayout:constraintlayout:$constraintLayout"

	// Material Design
	implementation "com.google.android.material:material:$materialDesign"

	implementation "com.google.dagger:dagger:$dagger"
	kapt "com.google.dagger:dagger-compiler:$dagger"

	implementation project(':service')
}

