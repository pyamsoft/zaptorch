/*
 * Copyright 2020 Peter Kenji Yamanaka
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.


buildscript {
	ext {
		kotlin_version = '1.4.21'
	}
	repositories {
		google()
		mavenCentral()
		jcenter()
		mavenLocal()

		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}

	ext {

		minSdk = 23
		maxSdk = 30

		// Logging
		timber = "4.7.1"

		// AndroidX
		core = "1.3.2"
		preference = "1.1.1"
		coordinatorLayout = "1.1.0"
		constraintLayout = "2.0.4"

		// Material Design
		materialDesign = "1.2.1"

		pydroid = "22.1.2"
		leakCanary = "2.5"
		dagger = "2.30.1"
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:4.1.1'

		// Gradle version checker
		classpath 'com.github.ben-manes:gradle-versions-plugin:0.29.0'

		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

		// Spotless
		classpath "com.diffplug.spotless:spotless-plugin-gradle:5.8.2"
	}
}

allprojects {
	apply plugin: "com.github.ben-manes.versions"
	apply plugin: "com.diffplug.spotless"

	dependencyUpdates.resolutionStrategy {
		componentSelection { rules ->
			rules.all { ComponentSelection selection ->
				boolean rejected = [
					'alpha',
					'beta',
					'rc',
					'cr',
					'm',
					'preview'
				].any { qualifier ->
					selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
				}
				if (rejected) {
					selection.reject('Release candidate')
				}
			}
		}
	}

	spotless {
		java {
			target "src/**/*.java"
		}
		kotlin {
			target "src/**/*.kt"
			ktlint()
		}
		groovyGradle {
			target "*.gradle"
			greclipse()
		}
	}

	repositories {
		google()
		mavenCentral()
		jcenter()
		mavenLocal()

		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}

	tasks.withType(JavaCompile) {
		options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
		options.deprecation = true
	}
}
