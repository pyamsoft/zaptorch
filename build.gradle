/*
 * Copyright 2016 Peter Kenji Yamanaka
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

tasks.whenTaskAdded { task ->
  if (task.name.startsWith("lint")) {
    task.enabled = false
  }
}

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    // Retro lambda
    classpath 'me.tatarka:gradle-retrolambda:3.3.0-beta4'

    // Dex count
    classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.5.0'

    // Android APT
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'

    // Gradle version checked
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.12.0'

    // Facebook ReDex
    classpath 'au.com.timmutton:redex:1.1.0'
  }
}

apply plugin: 'com.android.application'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'redex'

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.3"

  defaultConfig {
    applicationId "com.pyamsoft.zaptorch"
    minSdkVersion 18
    targetSdkVersion 23
    versionCode 6
    versionName "1.0.3"
    // For vector drawables
    vectorDrawables.useSupportLibrary = true
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  signingConfigs {
    release {
      //noinspection GroovyAssignabilityCheck
      storeFile new File(RELEASE_STORE_FILE)

      //noinspection GroovyAssignabilityCheck
      keyAlias RELEASE_KEY_ALIAS

      //noinspection GroovyAssignabilityCheck
      keyPassword RELEASE_KEY_PASSWD

      //noinspection GroovyAssignabilityCheck
      storePassword RELEASE_STORE_PASSWD
    }
  }

  buildTypes {
    release {
      //noinspection GroovyAssignabilityCheck
      signingConfig signingConfigs.release
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
          'proguard-rules.pro'
    }
  }

  lintOptions {
    disable 'InvalidPackage'
  }

  packagingOptions {
    exclude 'META-INF/services/javax.annotation.processing.Processor'
  }
}

android.applicationVariants.all { variant ->
  if (variant.buildType.name == 'release') {
    variant.outputs.each { output ->
      output.assemble.doLast {
        println "Zopflifying... it might take a while"
        exec {
          commandLine output.zipAlign.zipAlignExe, '-f', '-z', '4', output.outputFile.absolutePath,
              output.outputFile.absolutePath.replaceAll('\\.apk$', '-zopfli.apk')
        }
      }
    }
  }
}

retrolambda {
  jvmArgs '-noverify'
}

redex {
  passes = ["ReBindRefsPass",
            //    Doesn't work yet with Retrolambda
            //    "BridgePass",
            "SynthPass",
            "FinalInlinePass",
            "DelSuperPass",
            "SingleImplPass",
            "SimpleInlinePass",
            "StaticReloPass",
            "RemoveEmptyClassesPass",
            "ShortenSrcStringsPass"]
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  testCompile 'junit:junit:4.12'

  retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.3.0'

  // Dagger2
  // Dagger 2 and Compiler
  compile 'com.google.dagger:dagger:2.4'
  apt "com.google.dagger:dagger-compiler:2.4"

  // Butterknife
  compile 'com.jakewharton:butterknife:8.0.1'
  apt 'com.jakewharton:butterknife-compiler:8.0.1'

  // Common deps
  compile project(':PYDroid')
}

