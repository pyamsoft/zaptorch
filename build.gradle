/*
 * Copyright 2020 Peter Kenji Yamanaka
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.


buildscript {
  ext {
    kotlin_version = '1.5.20'
  }
  repositories {
    mavenLocal()
    google()
    mavenCentral()

    maven {
      url "https://jitpack.io"
    }

    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  ext {

    minSdk = 23
    maxSdk = 30

    // Logging
    timber = "4.7.1"

    // AndroidX
    core = "1.5.0"
    preference = "1.1.1"
    coordinatorLayout = "1.1.0"
    constraintLayout = "2.0.4"

    // Material Design
    materialDesign = "1.3.0"

    pydroid = "23.0.5"
    leakCanary = "2.7"
    dagger = "2.37"
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:4.2.2'

    // Gradle version checker
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.39.0'

    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

    // Spotless
    classpath "com.diffplug.spotless:spotless-plugin-gradle:5.14.0"
  }
}

allprojects {
  apply plugin: "com.github.ben-manes.versions"
  apply plugin: "com.diffplug.spotless"

  // Ignore non-release libraries
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
  }

  tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
      isNonStable(it.candidate.version)
    }
  }

  spotless {
    java {
      target "src/**/*.java"

      removeUnusedImports()
      trimTrailingWhitespace()
      endWithNewline()
      indentWithSpaces(2)
    }
    kotlin {
      target "src/**/*.kt"
      ktfmt()

      trimTrailingWhitespace()
      endWithNewline()
      indentWithSpaces(2)
    }
    groovyGradle {
      target "*.gradle"
      greclipse()

      trimTrailingWhitespace()
      endWithNewline()
      indentWithSpaces(2)
    }
  }

  repositories {
    mavenLocal()
    google()
    mavenCentral()

    maven {
      url "https://jitpack.io"
    }

    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
    options.deprecation = true
  }
}
